#lang racket/base

(require "interface.rkt"
         racket/class
         racket/set
         data/interval-map
         "../interfaces.rkt"
         "../responses.rkt"
         "../path-util.rkt"
         drracket/check-syntax)

(provide diag%)

(define diag%
  (class base-service%
    (init-field doc-text)
    (super-new)

    (define warn-diags (mutable-seteq))
    (define quickfixs (make-interval-map))
    (define unused-reqs (make-hasheq))

    (define/override (get)
      (list warn-diags quickfixs unused-reqs))

    (define/override (reset)
      (set-clear! warn-diags)
      (set! quickfixs (make-interval-map))
      (hash-clear! unused-reqs))

    (define/override (syncheck:add-mouse-over-status src-obj start finish text)
      (when (string=? "no bound occurrences" text)
        (hint-unused-variable src-obj start finish)))

    ;; Mouse-over status
    (define (hint-unused-variable src-obj start finish)
      (unless (string=? "_" (send doc-text get-text start (add1 start)))
        (define diag
          (Diagnostic #:range (Range #:start (abs-pos->Pos doc-text start)
                                     #:end (abs-pos->Pos doc-text finish))
                      #:severity Diag-Information
                      #:source (path->uri src-obj)
                      #:message "unused variable"))

        (define code-action
          (CodeAction
           #:title "Add prefix `_` to ignore"
           #:kind "quickfix"
           #:diagnostics (list diag)
           #:edit (WorkspaceEdit
                   #:changes
                   (hasheq (string->symbol (path->uri src-obj))
                           (list (TextEdit #:range (Range #:start (abs-pos->Pos doc-text start)
                                                          #:end (abs-pos->Pos doc-text start))
                                           #:newText "_"))))))
        (interval-map-set! quickfixs start (add1 finish) code-action)
        (set-add! warn-diags diag)))

    (define/override (syncheck:add-unused-require _src start end)
      (define range
        (Range #:start (abs-pos->Pos doc-text start)
               #:end (abs-pos->Pos doc-text end)) )
      (define diag (Diagnostic #:range range
                               #:severity Diag-Information
                               #:source "Racket"
                               #:message "unused require"))
      (set-add! warn-diags diag)
      (hash-set! unused-reqs start end))))

